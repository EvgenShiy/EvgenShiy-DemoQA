plugins {
    id 'java'
    id 'java-library'
    id 'io.qameta.allure' version '2.12.0'
    id "io.freefair.lombok" version "8.11"
}

repositories {
    mavenCentral()
}

def selenideVersion = "7.6.1",
    restAssuredVersion = "5.3.1",
    allureVersion = "2.29.0",
    aspectjVersion = "1.9.21"

allure {
    report {
        version.set(allureVersion) // Общая версия Allure для API и UI тестов
    }
    adapter {
        aspectjWeaver.set(true) // Включаем поддержку аннотаций @Step
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    // Общие зависимости
    implementation(
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "org.junit.jupiter:junit-jupiter:5.10.0",
            "org.slf4j:slf4j-simple:2.0.16",
            "org.aeonbits.owner:owner:1.0.12",
            "org.slf4j:slf4j-api:2.0.16",
            "org.aspectj:aspectjweaver:$aspectjVersion",
            "org.aspectj:aspectjrt:$aspectjVersion",
            "org.yaml:snakeyaml:1.33"
    )

    // API-specific dependencies
    testImplementation(
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2",
            "org.assertj:assertj-core:3.24.2"
    )

    // UI-specific dependencies
    testImplementation(
            'com.github.javafaker:javafaker:1.0.2',
            'io.github.bonigarcia:webdrivermanager:5.9.2'
    )

    // Runtime dependencies
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.8.2')
}

tasks.withType(Test).configureEach {
    systemProperties(System.getProperties())
    systemProperty "env", System.getProperty("env")
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

// Разделение API и UI тестов по тегам
tasks.register('api', Test) {
    useJUnitPlatform {
        includeTags("API")
    }
}

tasks.register('ui', Test) {
    useJUnitPlatform {
        includeTags("UI")
    }
}

println "TestType: ${project.hasProperty('TestType') ? project.TestType : 'not set'}"

// Кодировка UTF-8 для компиляции
compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}